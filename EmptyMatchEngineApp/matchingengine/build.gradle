apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

apply plugin: 'com.jfrog.artifactory'

apply plugin: 'org.ysb33r.doxygen'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// Stop build if the proto definitions git repo is missing (via absolute path):
def edgeProtoGitRepo = "$projectDir/../../../edge-proto"
// Sanity check:
if (!new File(edgeProtoGitRepo).exists() ) {
    throw new Exception("Invalid repo. Please git clone the shared edge-proto.git repository ($edgeProtoGitRepo) at the same directory level as this git repo.")
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 27
        versionName "3.0.8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            proto {
                srcDirs "${edgeProtoGitRepo}/dme"
                srcDirs "${edgeProtoGitRepo}/third_party/googleapis"
                srcDirs "${edgeProtoGitRepo}/edgeprotogen"
            }
            java {
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc {
                    option 'lite'
                }
            }
        }
    }
}



dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.google.code.gson:gson:2.8.6'
    androidTestImplementation "com.squareup.okio:okio:1.13.0"

    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
    implementation "io.grpc:grpc-okhttp:${grpcVersion}"

    implementation 'org.apache.tomcat:annotations-api:6.0.53'

    // For Google Services
    implementation 'com.google.android.gms:play-services-location:19.0.0'

    // For JWT validation
    implementation 'com.auth0.android:jwtdecode:2.0.0'
}

artifactoryPublish.dependsOn('build')
publishing {
    publications {
        aar(MavenPublication) {
            groupId = "com.mobiledgex"
            artifactId = project.getName()
            version = android.defaultConfig.versionName

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            pom.withXml {
                asNode().with {
                    appendNode('name', 'matchingengine')
                    appendNode('description', 'MobiledgeX MatchingEngine library to access your global edge cloud application resources')
                    appendNode('url', 'https://github.com/mobiledgex/edge-cloud-sdk-android')
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'The Apache Software License, Version 2.0')
                            appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.txt')
                        }
                    }
                    appendNode('developers').with {
                        appendNode('developer').with {
                            appendNode('name', 'MobiledgeX, Inc.')
                        }
                    }
                }
            }
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.aar)
}

doxygen {
    template            new File(projectDir, '../doxygen/Doxyfile')
    project_name        'Android SDK'
    source              new File(projectDir,'src/main/java/com/mobiledgex/matchingengine'), new File(projectDir, '../doxygen/src/DoxygenModules.txt')
    outputDir           new File(projectDir,'../../')
    exclude             'build/'
    file_paterns        '*.java', '*.class','*.md'
    recursive           true
}
